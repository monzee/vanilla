apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 25
    buildToolsVersion "24.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "v$LIBRARY_VERSION"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

android.libraryVariants.all { variant ->
    if (!"release".equals(variant.buildType.name)) {
        return
    }

    task("${variant.name}Javadoc", type: Javadoc, dependsOn: "assemble${variant.name.capitalize()}") {
        source = variant.javaCompile.source

        // First add all of your dependencies to the classpath, then add the android jars
        classpath += files(variant.javaCompile.classpath.files)
        classpath += files(android.getBootClasspath())

        options {
            links '../../../../vanilla-core/build/docs/javadoc'
            linksOffline('https://developer.android.com/reference', 'http://docs.oracle.com/javase/7/docs/api')
        }

        // We're excluding these generated files
        exclude '**/BuildConfig.java'
        exclude '**/R.java'
    }

    task("${variant.name}JavadocJar", type: Jar, dependsOn: "${variant.name}Javadoc") {
        classifier = 'javadoc'
        from tasks["${variant.name}Javadoc"].destinationDir
    }

    publishing.publications.release.artifact(tasks["${variant.name}JavadocJar"])
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

publishing {
    repositories {
        maven {
            name "tmp"
            url "file:///tmp"
        }
    }

    publications {
        release(MavenPublication) {
            groupId 'ph.codeia.vanilla'
            artifactId 'vanilla-android'
            version LIBRARY_VERSION

            artifact "$buildDir/outputs/aar/vanilla-android-release.aar"
            artifact sourceJar
            pom.withXml {
                def node = asNode().appendNode('dependencies')
                def core = node.appendNode('dependency')
                core.appendNode('groupId', 'ph.codeia.vanilla')
                core.appendNode('artifactId', 'vanilla-core')
                core.appendNode('version', LIBRARY_VERSION)
                configurations.compile.allDependencies.each { dep ->
                    if (dep instanceof ExternalDependency) {
                        def n = node.appendNode('dependency')
                        n.appendNode('groupId', dep.group)
                        n.appendNode('artifactId', dep.name)
                        n.appendNode('version', dep.version)
                    }
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':vanilla-core')
    provided 'com.android.support:appcompat-v7:25.0.0'
    testCompile 'junit:junit:4.12'
}
