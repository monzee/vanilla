apply plugin: 'java'
apply plugin: 'maven-publish'

group = 'ph.codeia.vanilla'
version = LIBRARY_VERSION

targetCompatibility = 1.7
sourceCompatibility = 1.7

dependencies {
}

task sourceJar(type: Jar, dependsOn: 'classes', group: 'publishing') {
    classifier 'sources'
    from sourceSets.main.allSource
}

javadoc {
    title = 'Vanilla Core'
    options.links 'https://developer.android.com/reference'
}

task docJar(type: Jar, dependsOn: 'javadoc', group: 'publishing') {
    classifier 'javadoc'
    from javadoc.destinationDir
}

publishing {
    repositories {
        maven {
            name 'tmp'
            url 'file:///tmp'
        }

        maven {
            name 'bintray'
            url 'https://api.bintray.com/maven/monzee/jvm/vanilla-core/'
            credentials {
                username = BINTRAY_USER
                password = BINTRAY_API_KEY
            }
        }
    }

    publications {
        core(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact docJar
        }
    }
}

