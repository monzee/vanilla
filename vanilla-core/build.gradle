apply plugin: 'java'
apply plugin: 'maven-publish'

targetCompatibility = 1.7
sourceCompatibility = 1.7

dependencies {
}

javadoc {
    title = 'Vanilla Core'
    options.links 'https://developer.android.com/reference'
}

task jarSources(type: Jar, dependsOn: 'classes', group: 'publishing') {
    classifier 'sources'
    from sourceSets.main.allSource
}

task jarJavadoc(type: Jar, dependsOn: 'javadoc', group: 'publishing') {
    classifier 'javadoc'
    from javadoc.destinationDir
}

publishing {
    repositories.maven {
        name 'bintray'
        url 'https://api.bintray.com/maven/monzee/jvm/vanilla-core/'
        credentials {
            username = BINTRAY_USER
            password = BINTRAY_API_KEY
        }
    }

    publications {
        core(MavenPublication) {
            from components.java
            artifact jarSources
            artifact jarJavadoc

            pom.withXml {
                asNode().children().last() + pomMetadata(
                    name: 'Vanilla Core',
                    description: 'Generic interfaces for functional programming and a few other things',
                    url: 'https://github.com/monzee/vanilla')
            }
        }

    }
}
